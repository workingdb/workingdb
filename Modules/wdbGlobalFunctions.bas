Option Compare Database
Option Explicit

Public bClone As Boolean

Declare PtrSafe Sub ChooseColor Lib "msaccess.exe" Alias "#53" (ByVal hwnd As LongPtr, rgb As Long)
Declare PtrSafe Function LoadCursorFromFile Lib "user32" Alias "LoadCursorFromFileA" (ByVal lpFileName As String) As Long
Declare PtrSafe Function setCursor Lib "user32" Alias "SetCursor" (ByVal hCursor As Long) As Long

Function setSplashLoading(label As String)
On Error GoTo Err_Handler

If IsNull(TempVars!loadAmount) Then Exit Function
TempVars.Add "loadAmount", TempVars!loadAmount + 1
Form_frmSplash.lnLoading.Width = (TempVars!loadAmount / 12) * TempVars!loadWd
Form_frmSplash.lblLoading.Caption = label
Form_frmSplash.Repaint

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "setSplashLoading", Err.DESCRIPTION, Err.number)
End Function

Function openNotificationFromEmail()
On Error GoTo Err_Handler

Dim olApp As Object
Dim olInspector As Object
Dim olMail As Object
Dim emailBody As String

Set olApp = GetObject(, "Outlook.Application")

If olApp.ActiveInspector Is Nothing Then
    Set olMail = olApp.ActiveExplorer.Selection.ITEM(1)
Else
    Set olMail = olApp.ActiveInspector.CurrentItem
End If

emailBody = olMail.htmlBody

Set olMail = Nothing
Set olInspector = Nothing
Set olApp = Nothing

Dim appName As String
Dim ID As String

appName = Split(Split(emailBody, "AppName:[")(1), "]")(0)
ID = Split(Split(emailBody, "AppId:[")(1), "]")(0)

Select Case appName
    Case "Design WO"
        If CurrentProject.AllForms("frmDRSdashboard").IsLoaded = True Then
             DoCmd.CLOSE acForm, "frmDRSdashboard"
                  On Error Resume Next
            TempVars.Add "controlNumber", ID
            DoCmd.OpenForm "frmDRSdashboard"
        Else
            TempVars.Add "controlNumber", ID
            DoCmd.OpenForm "frmDRSdashboard"
        End If
    Case "Part Project"
        openPartProject (ID)
    Case "Issue"
        DoCmd.OpenForm "frmPartIssues", , , "recordId = " & ID
    Case "Trial"
        DoCmd.OpenForm "frmPartTrialDetails", , , "recordId = " & ID
End Select

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "openNotificationFromEmail", Err.DESCRIPTION, Err.number)
End Function

Function setCustomCursor()
Dim lngRet As Long
lngRet = LoadCursorFromFile("\\data\mdbdata\WorkingDB\Pictures\Theme_Pictures\cursor.cur")
lngRet = setCursor(lngRet)
End Function

Function getCustomerName(customerId As Long) As String
On Error GoTo Err_Handler

getCustomerName = ""

Dim db As Database
Set db = CurrentDb()

Dim rs As Recordset
Set rs = db.OpenRecordset("SELECT CUSTOMER_NAME FROM APPS_XXCUS_CUSTOMERS WHERE CUSTOMER_ID = " & customerId)

If rs.RecordCount > 0 Then getCustomerName = rs!CUSTOMER_NAME

rs.CLOSE
Set rs = Nothing
Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "getCustomerName", Err.DESCRIPTION, Err.number)
End Function

Function dueDay(dueDate, completeddate) As String
On Error Resume Next

If IsNull(dueDate) Then
    dueDay = "N/A"
    Exit Function
End If

If IsNull(completeddate) Then
    Select Case dueDate
        Case Date
            dueDay = "Today"
        Case Date + 1
            dueDay = "Tomorrow"
        Case Is < Date
            dueDay = "Overdue"
        Case Is < Date + 7
            dueDay = WeekdayName(Weekday(dueDate))
        Case Date + 7
            dueDay = "1 Week"
        Case Is < Date + 14
            dueDay = "<2 Weeks"
        Case Date + 14
            dueDay = "2 Weeks"
        Case Is < Date + 21
            dueDay = "<3 Weeks"
        Case Date + 21
            dueDay = "3 Weeks"
        Case Is < Date + 28
            dueDay = "<4 Weeks"
        Case Date + 28
            dueDay = "4 Weeks"
        Case Is > Date + 28
            dueDay = ">4 Weeks"
        Case Else
            dueDay = dueDate
    End Select
Else
    dueDay = "Complete"
End If

End Function

Function dbExecute(sql As String)
On Error GoTo Err_Handler

Dim db As Database
Set db = CurrentDb()

db.Execute sql

Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "dbExecute", Err.DESCRIPTION, Err.number, sql)
End Function

Function dbPGExecute(sql As String)
On Error GoTo Err_Handler

Dim db As Database
Set db = CurrentDb()

Dim qdf As QueryDef, tempRS As Recordset

Set qdf = db.QueryDefs("dbPGExecute")
qdf.sql = sql
    
db.QueryDefs.refresh

qdf.Execute

Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "dbExecute", Err.DESCRIPTION, Err.number, sql)
End Function

Function findDescription(partNumber As String) As String
On Error GoTo Err_Handler

findDescription = ""

'first, check Oracle, then check SIFs

Dim db As Database
Dim rs1 As Recordset
Set db = CurrentDb
Set rs1 = db.OpenRecordset("SELECT SEGMENT1, DESCRIPTION FROM APPS_MTL_SYSTEM_ITEMS WHERE SEGMENT1 = '" & partNumber & "'", dbOpenSnapshot)
If rs1.RecordCount = 0 Then 'not in main Oracle table, now look through SIFs
    If DCount("[ROW_ID]", "APPS_Q_SIF_NEW_ASSEMBLED_PART_V", "[NIFCO_PART_NUMBER] = '" & partNumber & "'") > 0 Then 'is it in assy table?
        Set rs1 = db.OpenRecordset("SELECT SIFNUM, PART_DESCRIPTION FROM APPS_Q_SIF_NEW_ASSEMBLED_PART_V WHERE NIFCO_PART_NUMBER = '" & partNumber & "'", dbOpenSnapshot)
        rs1.MoveLast
        findDescription = rs1!PART_DESCRIPTION
    ElseIf DCount("[ROW_ID]", "APPS_Q_SIF_NEW_MOLDED_PART_V ", "[NIFCO_PART_NUMBER] = '" & partNumber & "'") > 0 Then 'is it in molded table?
        Set rs1 = db.OpenRecordset("SELECT SIFNUM, PART_DESCRIPTION FROM APPS_Q_SIF_NEW_MOLDED_PART_V WHERE NIFCO_PART_NUMBER = '" & partNumber & "'", dbOpenSnapshot)
        rs1.MoveLast
        findDescription = rs1!PART_DESCRIPTION
    ElseIf DCount("[ROW_ID]", "APPS_Q_SIF_NEW_PURCHASING_PART_V ", "[NIFCO_PART_NUMBER] = '" & partNumber & "'") > 0 Then 'is it in molded table?
        Set rs1 = db.OpenRecordset("SELECT SIFNUM, PART_DESCRIPTION FROM APPS_Q_SIF_NEW_PURCHASING_PART_V WHERE NIFCO_PART_NUMBER = '" & partNumber & "'", dbOpenSnapshot)
        rs1.MoveLast
        findDescription = rs1!PART_DESCRIPTION
    End If
    Exit Function
End If

findDescription = rs1("DESCRIPTION")

rs1.CLOSE
Set rs1 = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "findDescription", Err.DESCRIPTION, Err.number)
End Function

Function applyToAllForms()
Dim obj As AccessObject, dbs As Object
Set dbs = Application.CurrentProject
' Search for open AccessObject objects in AllForms collection.
For Each obj In dbs.AllForms
    If Left(obj.name, 1) = "f" Then
        If obj.name = "frmSearchHistory" Then GoTo nextOne
        DoCmd.OpenForm obj.name, acDesign
        
        If forms(obj.name).DefaultView = 1 Then
            forms(obj.name).BorderStyle = 2
        End If
        DoCmd.CLOSE acForm, obj.name, acSaveYes
    End If
nextOne:
Next obj

End Function

Public Function exportSQL(sqlString As String, FileName As String)
On Error Resume Next
Dim db As Database
Set db = CurrentDb()
db.QueryDefs.Delete "myExportQueryDef"
On Error GoTo Err_Handler

Dim qExport As DAO.QueryDef
Set qExport = db.CreateQueryDef("myExportQueryDef", sqlString)

DoCmd.TransferSpreadsheet acExport, acSpreadsheetTypeExcel12Xml, "myExportQueryDef", FileName, True
If MsgBox("Export Complete. File path: " & FileName & vbNewLine & "Do you want to open this file?", vbYesNo, "Notice") = vbYes Then openPath (FileName)

db.QueryDefs.Delete "myExportQueryDef"

Set db = Nothing
Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "exportSQL", Err.DESCRIPTION, Err.number)
End Function

Public Function nowString() As String
On Error GoTo Err_Handler

nowString = Format(Now(), "yyyymmddTHHmmss")

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "nowString", Err.DESCRIPTION, Err.number)
End Function

Public Function snackBox(sType As String, sTitle As String, sMessage As String, refForm As String, Optional centerBool As Boolean = False, Optional autoClose As Boolean = True)
On Error GoTo Err_Handler

TempVars.Add "snackType", sType
TempVars.Add "snackTitle", sTitle
TempVars.Add "snackMessage", sMessage
TempVars.Add "snackAutoClose", autoClose

If centerBool Then
    TempVars.Add "snackCenter", "True"
    TempVars.Add "snackLeft", forms(refForm).WindowLeft + forms(refForm).WindowWidth / 2 - 3393
    TempVars.Add "snackTop", forms(refForm).WindowTop + forms(refForm).WindowHeight / 2 - 500
Else
    TempVars.Add "snackCenter", "False"
    TempVars.Add "snackLeft", forms(refForm).WindowLeft + 200
    TempVars.Add "snackTop", forms(refForm).WindowTop + forms(refForm).WindowHeight - 1250
End If

DoCmd.OpenForm "frmSnack"

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "snackBox", Err.DESCRIPTION, Err.number)
End Function

Public Function labelUpdate(oldLabel As String)
On Error GoTo Err_Handler

Select Case True
    Case InStr(oldLabel, "-") <> 0
        labelUpdate = Replace(oldLabel, "-", ">")
    Case InStr(oldLabel, ">") <> 0
        labelUpdate = Replace(oldLabel, ">", "<")
    Case InStr(oldLabel, "<") <> 0
        labelUpdate = Replace(oldLabel, "<", "-")
End Select

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "labelUpdate", Err.DESCRIPTION, Err.number)
End Function

Public Function labelDirection(label As String)
On Error GoTo Err_Handler
If InStr(label, ">") <> 0 Then
    labelDirection = "DESC"
Else
    labelDirection = ""
End If
Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "labelDirection", Err.DESCRIPTION, Err.number)
End Function

Public Function registerWdbUpdates(table As String, ID As Variant, column As String, oldVal As Variant, newVal As Variant, Optional tag0 As String = "", Optional tag1 As Variant = "")
On Error GoTo Err_Handler

Dim sqlColumns As String, sqlValues As String

If (VarType(oldVal) = vbDate) Then oldVal = Format(oldVal, "mm/dd/yyyy")
If (VarType(newVal) = vbDate) Then newVal = Format(newVal, "mm/dd/yyyy")

Dim db As Database
Set db = CurrentDb()
Dim rs1 As Recordset
Set rs1 = db.OpenRecordset("tblWdbUpdateTracking")

If Len(oldVal) > 255 Then oldVal = Left(oldVal, 255)
If Len(newVal) > 255 Then newVal = Left(newVal, 255)

If VarType(ID) = vbString Then
    tag0 = ID
    ID = 0
End If

With rs1
    .addNew
        !tableName = table
        !tableRecordId = ID
        !updatedBy = Environ("username")
        !updatedDate = Now()
        !columnName = column
        !previousData = StrQuoteReplace(CStr(Nz(oldVal, "")))
        !newData = StrQuoteReplace(CStr(Nz(newVal, "")))
        !dataTag0 = StrQuoteReplace(tag0)
        !dataTag1 = StrQuoteReplace(tag1)
    .Update
    .Bookmark = .lastModified
End With

rs1.CLOSE
Set rs1 = Nothing
Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "registerWdbUpdates", Err.DESCRIPTION, Err.number, table & " " & ID)
End Function

Public Function registerSalesUpdates(table As String, ID As Variant, column As String, oldVal As Variant, newVal As Variant, Optional tag0 As String = "", Optional tag1 As Variant = "")
On Error GoTo Err_Handler

Dim sqlColumns As String, sqlValues As String

If (VarType(oldVal) = vbDate) Then oldVal = Format(oldVal, "mm/dd/yyyy")
If (VarType(newVal) = vbDate) Then newVal = Format(newVal, "mm/dd/yyyy")

Dim db As Database
Set db = CurrentDb()
Dim rs1 As Recordset
Set rs1 = db.OpenRecordset("tblSalesUpdateTracking")

With rs1
    .addNew
        !tableName = table
        !tableRecordId = ID
        !updatedBy = Environ("username")
        !updatedDate = Now()
        !columnName = column
        !previousData = StrQuoteReplace(CStr(Nz(oldVal, "")))
        !newData = StrQuoteReplace(CStr(Nz(newVal, "")))
        !dataTag0 = StrQuoteReplace(tag0)
        !dataTag1 = StrQuoteReplace(tag1)
    .Update
    .Bookmark = .lastModified
End With

rs1.CLOSE
Set rs1 = Nothing
Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "registerSalesUpdates", Err.DESCRIPTION, Err.number)
End Function

Function checkTime(whatIsHappening As String)

DoEvents

Dim tTime
tTime = Format$((Timer - TempVars!tStamp) * 100!, "0.00")

Debug.Print tTime & " " & whatIsHappening
TempVars.Add "tStamp", Timer

End Function

Public Function addWorkdays(dateInput As Date, daysToAdd As Long) As Date
On Error GoTo Err_Handler

Dim db As Database
Set db = CurrentDb()
Dim i As Long, testDate As Date, daysLeft As Long, rsHolidays As Recordset, intDirection
testDate = dateInput
daysLeft = Abs(daysToAdd)
intDirection = 1
If daysToAdd < 0 Then intDirection = -1

Set rsHolidays = db.OpenRecordset("tblHolidays")

Do While daysLeft > 0
    testDate = testDate + intDirection
    If Weekday(testDate) = 7 Or Weekday(testDate) = 1 Then ' IF WEEKEND -> skip
        testDate = testDate + intDirection
        GoTo skipDate
    End If
    
    rsHolidays.FindFirst "holidayDate = #" & testDate & "#"
    If Not rsHolidays.noMatch Then GoTo skipDate ' IF HOLIDAY -> skip to next da

     daysLeft = daysLeft - 1
skipDate:
Loop

addWorkdays = testDate

On Error Resume Next
rsHolidays.CLOSE
Set rsHolidays = Nothing
Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "addWorkdays", Err.DESCRIPTION, Err.number)
End Function

Public Function countWorkdays(oldDate As Date, newDate As Date) As Long
On Error GoTo Err_Handler

Dim total, sunday, saturday, weekdays, holidays

total = DateDiff("d", [oldDate], [newDate], vbSunday)
sunday = DateDiff("ww", [oldDate], [newDate], 1)
saturday = DateDiff("ww", [oldDate], [newDate], 7)
holidays = DCount("recordId", "tblHolidays", "holidayDate > #" & oldDate - 1 & "# AND holidayDate < #" & newDate & "#")
countWorkdays = total - sunday - saturday - holidays

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "countWorkdays", Err.DESCRIPTION, Err.number)
End Function

Function getFullName(Optional userName As String = "", Optional firstOnly As Boolean = False) As String
On Error GoTo Err_Handler

If userName = "" Then userName = Environ("username")

Dim db As Database
Set db = CurrentDb()
Dim rs1 As Recordset
Set rs1 = db.OpenRecordset("SELECT firstName, lastName FROM tblPermissions WHERE User = '" & userName & "'", dbOpenSnapshot)

If firstOnly Then
    getFullName = rs1!firstName
Else
    getFullName = rs1!firstName & " " & rs1!lastName
End If

rs1.CLOSE: Set rs1 = Nothing
Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "getFullName", Err.DESCRIPTION, Err.number)
End Function

Function notificationsCount()
On Error Resume Next

Dim db As Database
Set db = CurrentDb()
Dim rsNoti As Recordset
Set rsNoti = db.OpenRecordset("SELECT count(ID) as unRead FROM tblNotificationsSP WHERE recipientUser = '" & Environ("username") & "' AND readDate is null")

Select Case rsNoti!unRead
    Case Is > 9
        Form_DASHBOARD.Form.notifications.Caption = "9+"
        Form_DASHBOARD.Form.notifications.BackColor = rgb(230, 0, 0)
    Case 0
        Form_DASHBOARD.Form.notifications.Caption = CStr(rsNoti!unRead)
        Form_DASHBOARD.Form.notifications.BackColor = rgb(60, 170, 60)
    Case Else
        Form_DASHBOARD.Form.notifications.Caption = CStr(rsNoti!unRead)
        Form_DASHBOARD.Form.notifications.BackColor = rgb(230, 0, 0)
End Select

rsNoti.CLOSE
Set rsNoti = Nothing
Set db = Nothing

End Function

Function loadECOtype(changeNotice As String) As String
On Error GoTo Err_Handler

loadECOtype = ""

Dim db As Database
Set db = CurrentDb()
Dim rs1 As Recordset
Set rs1 = db.OpenRecordset("SELECT [CHANGE_ORDER_TYPE_ID] from ENG_ENG_ENGINEERING_CHANGES where [CHANGE_NOTICE] = '" & changeNotice & "'", dbOpenSnapshot)

If rs1.RecordCount > 0 Then loadECOtype = DLookup("[ECO_Type]", "[tblOracleDropDowns]", "[ECO_Type_ID]=" & rs1!CHANGE_ORDER_TYPE_ID)

rs1.CLOSE
Set rs1 = Nothing
Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "loadECOtype", Err.DESCRIPTION, Err.number)
End Function

Function getAvatarAPI(apiOption As String)
On Error GoTo Err_Handler

Dim FilePath As String, pngPath As String
Dim fileNumber As Integer

Dim svgContents As String
Dim reader As New XMLHTTP60
    reader.open "GET", "https://api.dicebear.com/9.x/" & apiOption & "/svg?seed=" & Environ("username") & "&radius=50", False
    reader.send
        Do Until reader.ReadyState = 4
            DoEvents
        Loop
If reader.status = 200 Then
    svgContents = reader.responseText
Else
    MsgBox reader.status
End If

FilePath = "\\data\mdbdata\WorkingDB\Pictures\Avatars\svg\" & Environ("username") & ".svg"
pngPath = "\\data\mdbdata\WorkingDB\Pictures\Avatars\" & Environ("username") & ".png"
fileNumber = FreeFile
Open FilePath For Output As #fileNumber
Print #fileNumber, Split(svgContents, ">")(0) & ">" & Split(svgContents, "</metadata>")(1)
Close #fileNumber

Call convertSVGtoPNG(FilePath, pngPath)

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "getAvatar", Err.DESCRIPTION, Err.number)
End Function

Function getAvatar(userName As String, initials As String)
On Error GoTo Err_Handler

Dim FilePath As String
Dim fileNumber As Integer

FilePath = "\\data\mdbdata\WorkingDB\Pictures\Avatars\svg\" & userName & ".svg"
fileNumber = FreeFile
Open FilePath For Output As #fileNumber

Dim randomR As Integer, randomG As Integer, randomB As Integer
Dim inputColor, tempHex, fullHex

randomR = randomNumber(30, 170)
randomG = randomNumber(30, 170)
randomB = randomNumber(30, 170)

'try to further randomize the color
Randomize
Select Case True
    Case randomR > randomG And randomR > randomB
        randomG = randomG * Rnd()
    Case randomG > randomB And randomG > randomR
        randomB = randomB * Rnd()
    Case Else
        randomR = randomR * Rnd()
End Select

inputColor = rgb(randomR, randomG, randomB)
tempHex = Hex(inputColor)
fullHex = Mid(tempHex, 5, 2) & Mid(tempHex, 3, 2) & Mid(tempHex, 1, 2)

Print #fileNumber, "<svg xmlns=""http://www.w3.org/2000/svg"" viewBox=""0 0 100 100""><mask id=""viewboxMask"">" & _
"<rect width=""100"" height=""100"" rx=""50"" ry=""50"" x=""0"" y=""0"" fill=""#fff"" /></mask><g mask=""url(#viewboxMask)""><rect fill=""#" & fullHex & """ widt" & _
"h=""100"" height=""100"" x=""0"" y=""0"" /><text x=""50%"" y=""50%"" font-family=""Arial, sans-serif"" font-size=""50"" font-" & _
"weight=""600"" fill=""#ffffff"" text-anchor=""middle"" dy=""17.800"">" & initials & "</text></g></svg>"

Close #fileNumber

Call convertSVGtoPNG(FilePath, "\\data\mdbdata\WorkingDB\Pictures\Avatars\" & userName & ".png")

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "getAvatar", Err.DESCRIPTION, Err.number)
End Function

Function convertSVGtoPNG(currentFile As String, newFile As String)
On Error GoTo Err_Handler

Dim ppt As New PowerPoint.Application
Dim pptPres As PowerPoint.Presentation
Dim curSlide As PowerPoint.Slide
Dim pptLayout As CustomLayout
Dim shp As PowerPoint.Shape

ppt.Presentations.Add
Set pptPres = ppt.ActivePresentation
Set pptLayout = pptPres.Designs(1).SlideMaster.CustomLayouts(7)
Set curSlide = pptPres.Slides.AddSlide(1, pptLayout)

Set shp = curSlide.Shapes.AddPicture(currentFile, msoFalse, msoTrue, 0, 0, 200, 200)

'shp.PictureFormat.TransparencyColor = rgb(255, 255, 255)
shp.export newFile, ppShapeFormatPNG

On Error Resume Next
pptPres.CLOSE
ppt.Quit
Set ppt = Nothing
Set pptPres = Nothing
Set curSlide = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "getAvatar", Err.DESCRIPTION, Err.number)
End Function

Function getAPI(url, header1, header2)
On Error GoTo Err_Handler

Dim reader As New XMLHTTP60
    reader.open "GET", url, False
    reader.setRequestHeader header1, header2
    reader.send
        Do Until reader.ReadyState = 4
            DoEvents
        Loop
If reader.status = 200 Then
    getAPI = reader.responseText
Else
    MsgBox reader.status
End If

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "getAPI", Err.DESCRIPTION, Err.number)
End Function

Function generateEmailWarray(Title As String, subTitle As String, primaryMessage As String, detailTitle As String, arr() As Variant, Optional addLines As Boolean = False) As String
On Error GoTo Err_Handler

Dim tblHeading As String, tblFooter As String, strHTMLBody As String, extraFooter As String, detailTable As String

Dim ITEM, i
i = 0
detailTable = ""
For Each ITEM In arr()
    If i = UBound(arr) Then
        detailTable = detailTable & "<tr style=""border-collapse: collapse;""><td style=""padding: .1em 2em 1em 2em;"">" & ITEM & "</td></tr>"
    Else
        detailTable = detailTable & "<tr style=""border-collapse: collapse;""><td style=""padding: .1em 2em;"">" & ITEM & "</td></tr>"
    End If
    i = i + 1
Next ITEM

tblHeading = "<table style=""width: 100%; margin: 0 auto; padding: 2em 3em; text-align: center; background-color: #fafafa;"">" & _
                            "<tbody>" & _
                                "<tr><td><h2 style=""color: #414141; font-size: 28px; margin-top: 0;"">" & Title & "</h2></td></tr>" & _
                                "<tr><td><p style=""color: rgb(73, 73, 73);"">" & subTitle & "</p></td></tr>" & _
                                 "<tr><td><table style=""padding: 1em; text-align: center;"">" & _
                                     "<tr><td style=""padding: 1em 1.5em; background: #FF6B00; "">" & primaryMessage & "</td></tr>" & _
                                "</table></td></tr>" & _
                            "</tbody>" & _
                        "</table>"
                        
tblFooter = "<table style=""width: 100%; margin: 0 auto; background: #2b2b2b; color: rgba(255,255,255,.5);"">" & _
                        "<tbody>" & _
                            "<tr style=""border-collapse: collapse;""><td style=""padding: 1em; color: #c9c9c9;"">" & detailTitle & "</td></tr>" & _
                            detailTable & _
                        "</tbody>" & _
                    "</table>"
                    
Dim addStuff As String
addStuff = ""
If addLines Then
    addStuff = "<table style=""max-width: 600px; margin: 0 auto; padding: 3em; background: #eaeaea; color: rgba(255,255,255,.5);"">" & _
        "<tr style=""border-collapse: collapse;""><td style=""padding: 1em;"">Extra Notes: type here...</td></tr></table>"
End If
                    
extraFooter = "<tr><td><p style=""color: rgb(192, 192, 192); text-align: center;"">This email address is not monitored, please do not reply to this email</p></td></tr>"

strHTMLBody = "" & _
"<!DOCTYPE html><html lang=""en"" xmlns=""http://www.w3.org/1999/xhtml"" xmlns:v=""urn:schemas-microsoft-com:vml"" xmlns:o=""urn:schemas-microsoft-com:office:office"">" & _
    "<head><meta charset=""utf-8""><title>Working DB Notification</title></head>" & _
    "<body style=""margin: 0 auto; Font-family: 'Montserrat', sans-serif; font-weight: 400; font-size: 15px; line-height: 1.8;"">" & addStuff & _
        "<table style=""max-width: 600px; margin: 0 auto; text-align: center; "">" & _
            "<tbody>" & _
                "<tr><td>" & tblHeading & "</td></tr>" & _
                "<tr><td>" & tblFooter & "</td></tr>" & _
                extraFooter & _
                "<tr><td><p style=""color: rgb(192, 192, 192); text-align: center;"">This email was created by  &copy; workingDB</p></td></tr>" & _
            "</tbody>" & _
        "</table>" & _
    "</body>" & _
"</html>"

generateEmailWarray = strHTMLBody

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "generateEmailWarray", Err.DESCRIPTION, Err.number)
End Function

Function generateHTML(Title As String, subTitle As String, primaryMessage As String, _
        detail1 As String, detail2 As String, detail3 As String, _
        Optional Link As String = "", _
        Optional addLines As Boolean = False, _
        Optional appName As String = "", _
        Optional appId As String = "") As String
        
On Error GoTo Err_Handler

Dim tblHeading As String, tblFooter As String, strHTMLBody As String

If Link <> "" Then
    primaryMessage = "<a href = '" & Link & "'>" & primaryMessage & "</a>"
ElseIf appId <> "" Then
    primaryMessage = "<a href = ""\\data\mdbdata\WorkingDB\build\workingdb_commands\openNotification.vbs"">" & primaryMessage & "</a>"
End If

tblHeading = "<table style=""width: 100%; margin: 0 auto; padding: 2em 3em; text-align: center; background-color: #fafafa;"">" & _
                            "<tbody>" & _
                                "<tr><td><h2 style=""color: #414141; font-size: 28px; margin-top: 0;"">" & Title & "</h2></td></tr>" & _
                                "<tr><td><p style=""color: rgb(73, 73, 73);"">" & subTitle & "</p></td></tr>" & _
                                 "<tr><td><table style=""padding: 1em; text-align: center;"">" & _
                                     "<tr><td style=""padding: 1em 1.5em; background: #FF6B00; "">" & primaryMessage & "</td></tr>" & _
                                "</table></td></tr>" & _
                            "</tbody>" & _
                        "</table>"
                        
tblFooter = "<table style=""width: 100%; margin: 0 auto; padding: 3em; background: #2b2b2b; color: rgba(255,255,255,.5);"">" & _
                        "<tbody>" & _
                            "<tr style=""border-collapse: collapse;""><td style=""padding: 1em; color: #c9c9c9;"">Details</td></tr>" & _
                            "<tr style=""border-collapse: collapse;""><td style=""padding: .1em 2em;"">" & detail1 & "</td></tr>" & _
                            "<tr style=""border-collapse: collapse;""><td style=""padding: .1em 2em;"">" & detail2 & "</td></tr>" & _
                            "<tr style=""border-collapse: collapse;""><td style=""padding: .1em 2em 1em 2em;"">" & detail3 & "</td></tr>" & _
                        "</tbody>" & _
                    "</table>"
                    
Dim addStuff As String
addStuff = ""
If addLines Then
    addStuff = "<table style=""max-width: 600px; margin: 0 auto; padding: 3em; background: #eaeaea; color: rgba(255,255,255,.5);"">" & _
        "<tr style=""border-collapse: collapse;""><td style=""padding: 1em;"">Extra Notes: type here...</td></tr></table>"
End If

strHTMLBody = "" & _
"<!DOCTYPE html><html lang=""en"" xmlns=""http://www.w3.org/1999/xhtml"" xmlns:v=""urn:schemas-microsoft-com:vml"" xmlns:o=""urn:schemas-microsoft-com:office:office"">" & _
    "<head><meta charset=""utf-8""><title>Working DB Notification</title></head>" & _
    "<body style=""margin: 0 auto; Font-family: 'Montserrat', sans-serif; font-weight: 400; font-size: 15px; line-height: 1.8;"">" & addStuff & _
        "<table style=""max-width: 600px; margin: 0 auto; text-align: center; "">" & _
            "<tbody>" & _
                "<tr><td>" & tblHeading & "</td></tr>" & _
                "<tr><td>" & tblFooter & "</td></tr>" & _
                "<tr><td><p style=""color: rgb(192, 192, 192); text-align: center;"">AppName:[" & appName & "], AppId:[" & appId & "]</p></td></tr>" & _
                "<tr><td><p style=""color: rgb(192, 192, 192); text-align: center;"">This email was created by  &copy; workingDB</p></td></tr>" & _
            "</tbody>" & _
        "</table>" & _
    "</body>" & _
"</html>"

generateHTML = strHTMLBody

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "generateHTML", Err.DESCRIPTION, Err.number)
End Function

Function dailySummary(Title As String, subTitle As String, lates() As String, todays() As String, nexts() As String, lateCount As Long, todayCount As Long, nextCount As Long) As String
On Error GoTo Err_Handler

Dim tblHeading As String, tblStepOverview As String, strHTMLBody As String

tblHeading = "<table style=""width: 100%; margin: 0 auto; padding: 2em 2em 1em 2em; text-align: center; background-color: #fafafa;"">" & _
                            "<tbody>" & _
                                "<tr><td><h2 style=""color: #414141; font-size: 28px; margin-top: 0;"">" & Title & "</h2></td></tr>" & _
                                "<tr><td><p style=""color: rgb(73, 73, 73);"">Here is what you have happening...</p></td></tr>" & _
                            "</tbody>" & _
                        "</table>"
                        
Dim i As Long, lateTable As String, todayTable As String, nextTable As String, varStr As String, varStr1 As String, seeMore As String
seeMore = "<tr style=""border-collapse: collapse;""><td style=""padding: .1em 2em; font-style: italic;"" colspan=""3"">see the rest in the workingdb...</td></tr>"
i = 0
tblStepOverview = ""

varStr = ""
varStr1 = ""
If lates(0) <> "" Then
    For i = 0 To UBound(lates)
        lateTable = lateTable & "<tr style=""border-collapse: collapse;"">" & _
                                                "<td style=""padding: .1em 2em;"">" & Split(lates(i), ",")(0) & "</td>" & _
                                                "<td style=""padding: .1em 2em;"">" & Split(lates(i), ",")(1) & "</td>" & _
                                                "<td style=""padding: .1em 2em;  color: rgb(255,195,195);"">" & Split(lates(i), ",")(2) & "</td></tr>"
    Next i
    If lateCount > 1 Then varStr = "s"
    If lateCount > 15 Then varStr1 = seeMore
    tblStepOverview = tblStepOverview & "<table style=""width: 100%; margin: 0 auto; background: #2b2b2b; color: rgb(255,255,255);""><tr><th style=""padding: 1em; font-size: 20px; color: rgb(255,150,150); display: table-header-group;"" colspan=""3"">You have " & _
                                                                lateCount & " item" & varStr & " overdue</th></tr><tbody>" & _
                                                            "<tr style=""padding: .1em 2em;""><th style=""text-align: left"">Part#</th><th style=""text-align: left"">Item</th><th style=""text-align: left"">Due</th></tr>" & lateTable & varStr1 & "</tbody></table>"
End If

varStr = ""
varStr1 = ""
If todays(0) <> "" Then
    For i = 0 To UBound(todays)
        todayTable = todayTable & "<tr style=""border-collapse: collapse;"">" & _
                                                "<td style=""padding: .1em 2em;"">" & Split(todays(i), ",")(0) & "</td>" & _
                                                "<td style=""padding: .1em 2em;"">" & Split(todays(i), ",")(1) & "</td>" & _
                                                "<td style=""padding: .1em 2em;"">" & Split(todays(i), ",")(2) & "</td></tr>"
    Next i
    If todayCount > 1 Then varStr = "s"
    If todayCount > 15 Then varStr1 = seeMore
    tblStepOverview = tblStepOverview & "<table style=""width: 100%; margin: 0 auto; background: #2b2b2b; color: rgb(255,255,255);""><tr><th style=""padding: 1em; font-size: 20px; color: rgb(235,200,200); display: table-header-group;"" colspan=""3"">You have " & _
                                                                todayCount & " item" & varStr & " due today</th></tr><tbody>" & _
                                                            "<tr style=""padding: .1em 2em;""><th style=""text-align: left"">Part#</th><th style=""text-align: left"">Item</th><th style=""text-align: left"">Due</th></tr>" & todayTable & varStr1 & "</tbody></table>"
End If

varStr = ""
varStr1 = ""
If nexts(0) <> "" Then
    For i = 0 To UBound(nexts)
        nextTable = nextTable & "<tr style=""border-collapse: collapse;"">" & _
                                                "<td style=""padding: .1em 2em;"">" & Split(nexts(i), ",")(0) & "</td>" & _
                                                "<td style=""padding: .1em 2em;"">" & Split(nexts(i), ",")(1) & "</td>" & _
                                                "<td style=""padding: .1em 2em;"">" & Split(nexts(i), ",")(2) & "</td></tr>"
    Next i
    If nextCount > 1 Then varStr = "s"
    If nextCount > 15 Then varStr1 = seeMore
    tblStepOverview = tblStepOverview & "<table style=""width: 100%; margin: 0 auto; background: #2b2b2b; color: rgb(255,255,255);""><tr><th style=""padding: 1em; font-size: 20px; color: rgb(235,235,235); display: table-header-group;"" colspan=""3"">You have " & _
                                                                nextCount & " item" & varStr & " due soon</th></tr><tbody>" & _
                                                            "<tr style=""padding: .1em 2em;""><th style=""text-align: left"">Part#</th><th style=""text-align: left"">Item</th><th style=""text-align: left"">Due</th></tr>" & nextTable & varStr1 & "</tbody></table>"
End If

strHTMLBody = "" & _
"<!DOCTYPE html><html lang=""en"" xmlns=""http://www.w3.org/1999/xhtml"" xmlns:v=""urn:schemas-microsoft-com:vml"" xmlns:o=""urn:schemas-microsoft-com:office:office"">" & _
    "<head><meta charset=""utf-8""><title>Working DB Notification</title></head>" & _
    "<body style=""margin: 0 auto; Font-family: 'Montserrat', sans-serif; font-weight: 400; font-size: 15px; line-height: 1.8;"">" & _
        "<table style=""max-width: 600px; margin: 0 auto; text-align: center; "">" & _
            "<tbody>" & _
                "<tr><td>" & tblHeading & "</td></tr>" & _
                "<tr><td>" & tblStepOverview & "</td></tr>" & _
                "<tr><td><p style=""color: rgb(192, 192, 192); text-align: center;"">If you wish to no longer receive these emails,  go into your account menu in the workingDB to disable daily summary notifications</p></td></tr>" & _
                "<tr><td><p style=""color: rgb(192, 192, 192); text-align: center;"">This email was created by  &copy; workingDB</p></td></tr>" & _
            "</tbody>" & _
        "</table>" & _
    "</body>" & _
"</html>"

dailySummary = strHTMLBody

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "dailySummary", Err.DESCRIPTION, Err.number)
End Function

Function emailContentGen(subject As String, Title As String, subTitle As String, primaryMessage As String, detail1 As String, detail2 As String, detail3 As String, Optional appName As String = "", Optional appId As String = "") As String
On Error GoTo Err_Handler

If appId <> "" Then
    primaryMessage = "<a href = ""\\data\mdbdata\WorkingDB\build\workingdb_commands\openNotification.vbs"">" & primaryMessage & "</a>"
End If

emailContentGen = subject & "," & Title & "," & subTitle & "," & primaryMessage & "," & detail1 & "," & detail2 & "," & detail3 & "," & appName & "," & appId

    Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "emailContentGen", Err.DESCRIPTION, Err.number)
End Function

Function sendNotification(sendTo As String, notType As Integer, notPriority As Integer, desc As String, emailContent As String, Optional appName As String = "", Optional appId As Variant = "", Optional multiEmail As Boolean = False, Optional customEmail As Boolean = False) As Boolean
sendNotification = True

On Error GoTo Err_Handler

Dim db As Database
Set db = CurrentDb()

'has this person been notified about this thing today already?
Dim rsNotifications As Recordset
Set rsNotifications = db.OpenRecordset("SELECT * from tblNotificationsSP WHERE recipientUser = '" & sendTo & "' AND notificationDescription = '" & StrQuoteReplace(desc) & "' AND sentDate > #" & Date - 1 & "#")
If rsNotifications.RecordCount > 0 Then
    If rsNotifications!notificationType = 1 Then
        Dim msgTxt As String
        If rsNotifications!senderUser = Environ("username") Then
            msgTxt = "You already nudged this person today"
        Else
            msgTxt = sendTo & " has already been nudged about this today by " & rsNotifications!senderUser & ". Let's wait until tomorrow to nudge them again."
        End If
        MsgBox msgTxt, vbInformation, "Hold on a minute..."
        sendNotification = False
        Exit Function
    End If
End If

Dim strEmail
If customEmail = False Then
    Dim ITEM, sendToArr() As String
    If multiEmail Then
        sendToArr = Split(sendTo, ",")
        strEmail = ""
        For Each ITEM In sendToArr
            If ITEM = "" Then GoTo nextItem
            strEmail = strEmail & getEmail(CStr(ITEM)) & ";"
nextItem:
        Next ITEM
        strEmail = Left(strEmail, Len(strEmail) - 1)
    Else
        strEmail = getEmail(sendTo)
    End If
Else
    strEmail = sendTo
    sendTo = Split(sendTo, "@")(0)
End If

Set rsNotifications = db.OpenRecordset("tblNotificationsSP")

With rsNotifications
    .addNew
    !recipientUser = sendTo
    !recipientEmail = strEmail
    !senderUser = Environ("username")
    !senderEmail = getEmail(Environ("username"))
    !sentDate = Now()
    !notificationType = notType
    !notificationPriority = notPriority
    !notificationDescription = desc
    !appName = appName
    !appId = appId
    !emailContent = emailContent
    .Update
End With

On Error Resume Next
rsNotifications.CLOSE
Set rsNotifications = Nothing
Set db = Nothing

Exit Function
Err_Handler:
sendNotification = False
    Call handleError("wdbGlobalFunctions", "sendNotification", Err.DESCRIPTION, Err.number)
End Function

Function privilege(pref) As Boolean
On Error GoTo Err_Handler

privilege = DLookup("[" & pref & "]", "[tblPermissions]", "[User] = '" & Environ("username") & "'")
    
Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "privilege", Err.DESCRIPTION, Err.number)
End Function

Function getTempFold() As String
On Error GoTo Err_Handler

getTempFold = Environ("temp") & "\workingdb\"
If FolderExists(getTempFold) = False Then MkDir (getTempFold)
    
Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "getTempFold", Err.DESCRIPTION, Err.number)
End Function

Function userData(data, Optional specificUser As String = "") As String
On Error GoTo Err_Handler

If specificUser = "" Then specificUser = Environ("username")

userData = Nz(DLookup("[" & data & "]", "[tblPermissions]", "[User] = '" & specificUser & "'"))

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "replaceDriveLetters", Err.DESCRIPTION, Err.number)
End Function

Public Function getTotalPackingListWeight(packId As Long) As Double
On Error Resume Next
getTotalPackingListWeight = 0

Dim db As Database
Set db = CurrentDb()
Dim rs1 As Recordset
Set rs1 = db.OpenRecordset("SELECT sum(unitWeight*quantity) as total FROM tblPackListChild WHERE packListId = " & packId & " GROUP BY packListId")

getTotalPackingListWeight = rs1!total

rs1.CLOSE
Set rs1 = Nothing
Set db = Nothing

End Function

Public Function getTotalPackingListCost(packId As Long) As Double
On Error Resume Next
getTotalPackingListCost = 0

Dim db As Database
Set db = CurrentDb()
Dim rs1 As Recordset
Set rs1 = db.OpenRecordset("SELECT sum(unitCost*quantity) as total FROM tblPackListChild WHERE packListId = " & packId & " GROUP BY packListId")

getTotalPackingListCost = rs1!total

rs1.CLOSE
Set rs1 = Nothing
Set db = Nothing

End Function

Function restrict(userName As String, dept As String, Optional reqLevel As String = "", Optional orAbove As Boolean = False) As Boolean
On Error GoTo Err_Handler

If (CurrentProject.Path <> "C:\workingdb") Then
    If userData("Developer") Then
        restrict = False
        Exit Function
    End If
End If

Dim db As Database
Set db = CurrentDb()
Dim d As Boolean, l As Boolean, rsPerm As Recordset
d = True
l = True

Set rsPerm = db.OpenRecordset("SELECT * FROM tblPermissions WHERE user = '" & userName & "'")
'restrict = true means you cannot access
'set No Access first, then allow as it is OK

If Nz(rsPerm!dept) = "" Or Nz(rsPerm("level")) = "" Then GoTo setRestrict 'if person isnt fully set up, do not allow access

If (rsPerm!dept = dept) Then
    d = False 'if correct department, set d to false
ElseIf rsPerm!dept = "Project" And dept = "CPC" And rsPerm("level") = "Manager" Then ' Project has same permissions as CPC for Managers Only
    d = False 'if correct department, set d to false
End If

Select Case True 'figure out level
    Case reqLevel = "" 'if level isn't specified, this doesn't matter! - allow
        l = False
    Case rsPerm("level") = reqLevel 'if the level matches perfectly, allow
        l = False
    Case orAbove And reqLevel = "Supervisor" 'if supervisor and above check level and both supervisors and managers
        If rsPerm("level") = "Supervisor" Or rsPerm("level") = "Manager" Then l = False
    Case orAbove And reqLevel = "Engineer" 'if engineer and above, check level
        If rsPerm("level") = "Engineer" Or rsPerm("level") = "Supervisor" Or rsPerm("level") = "Manager" Then l = False
End Select

setRestrict:
restrict = d Or l

rsPerm.CLOSE
Set rsPerm = Nothing
Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "restrict", Err.DESCRIPTION, Err.number)
End Function

Public Sub checkForFirstTimeRun()
On Error GoTo Err_Handler

Dim db As Database
Set db = CurrentDb()
Dim rsAnalytics As Recordset, rsSummaryEmail As Recordset

Set rsAnalytics = db.OpenRecordset("SELECT max(dateUsed) as anaDate from tblAnalytics WHERE module = 'firstTimeRun'")
If Not DateSerial(Year(rsAnalytics!anaDate), Month(rsAnalytics!anaDate), Day(rsAnalytics!anaDate)) >= Date Then
    'if max date is today, then this has already ran.
    Call checkProgramEvents
    Call scanSteps("all", "firstTimeRun")
    db.Execute "INSERT INTO tblAnalytics (module,form,userName,dateUsed) VALUES ('firstTimeRun','Form_frmSplash','" & Environ("username") & "','" & Now() & "')"
End If

If Weekday(Date) = 1 Or Weekday(Date) = 7 Then Exit Sub 'only run summaries on weekdays

Set rsSummaryEmail = db.OpenRecordset("SELECT max(dateUsed) as anaDate from tblAnalytics WHERE module = 'summaryEmail'")
If Not DateSerial(Year(rsSummaryEmail!anaDate), Month(rsSummaryEmail!anaDate), Day(rsSummaryEmail!anaDate)) >= Date Then Call openPath("\\data\mdbdata\WorkingDB\build\workingdb_commands\summaryEmail.vbs")

On Error Resume Next
rsAnalytics.CLOSE: Set rsAnalytics = Nothing
rsSummaryEmail.CLOSE: Set rsSummaryEmail = Nothing
Set db = Nothing

Exit Sub
Err_Handler:
    Call handleError("wdbGlobalFunctions", "checkForFirstTimeRun", Err.DESCRIPTION, Err.number)
End Sub

Function grabSummaryInfo(Optional specificUser As String = "") As Boolean
On Error GoTo Err_Handler

grabSummaryInfo = False

Dim db As Database
Set db = CurrentDb()
Dim rsPeople As Recordset, rsOpenSteps As Recordset, rsOpenWOs As Recordset, rsNoti As Recordset, rsAnalytics As Recordset, rsUserSettings As Recordset
Dim lateSteps() As String, todaySteps() As String, nextSteps() As String
Dim li As Long, ti As Long, ni As Long
Dim strQry, ranThisWeek As Boolean
Dim recordsetName As String

Set rsAnalytics = db.OpenRecordset("SELECT max(dateUsed) as anaDate from tblAnalytics WHERE module = 'firstTimeRun'")
ranThisWeek = Format(rsAnalytics!anaDate, "ww", vbMonday, vbFirstFourDays) = Format(Date, "ww", vbMonday, vbFirstFourDays)

strQry = ""
If specificUser <> "" Then strQry = " AND user = '" & specificUser & "'"

Set rsPeople = db.OpenRecordset("SELECT * from tblPermissions WHERE Inactive = False" & strQry)

    li = 0
    ti = 0
    ni = 0
    ReDim Preserve lateSteps(li)
    ReDim Preserve todaySteps(ti)
    ReDim Preserve nextSteps(ni)

Do While Not rsPeople.EOF 'go through every active person
    Set rsUserSettings = db.OpenRecordset("SELECT * from tblUserSettings WHERE username = '" & rsPeople!User & "'")

    If rsUserSettings!notifications = 1 And specificUser = "" Then GoTo nextPerson 'this person wants no notifications
    If rsUserSettings!notifications = 2 And ranThisWeek And specificUser = "" Then GoTo nextPerson 'this person only wants weekly notifications
    
    li = 0
    ti = 0
    ni = 0
    Erase lateSteps, todaySteps, nextSteps
    ReDim lateSteps(li)
    ReDim todaySteps(ti)
    ReDim nextSteps(ni)
    
    If rsPeople!Level = "Engineer" Then
        recordsetName = "SELECT * FROM qryStepApprovalTracker"
    Else
        recordsetName = "SELECT * FROM sqryStepApprovalTracker_Approvals_SupervisorsUp"
    End If

    Set rsOpenSteps = db.OpenRecordset(recordsetName & _
                                " WHERE person = '" & rsPeople!User & "' AND due <= Date()+7")
    
    Do While (Not rsOpenSteps.EOF And Not (ti > 15 And li > 15 And ni > 15))
        Select Case rsOpenSteps!Due
            Case Date 'due today
                If ti > 15 Then
                    ti = ti + 1
                    GoTo nextStep
                End If
                ReDim Preserve todaySteps(ti)
                todaySteps(ti) = rsOpenSteps!partNumber & "," & rsOpenSteps!Action & ",Today"
                ti = ti + 1
            Case Is < Date 'over due
                If li > 15 Then
                    li = li + 1
                    GoTo nextStep
                End If
                ReDim Preserve lateSteps(li)
                lateSteps(li) = rsOpenSteps!partNumber & "," & rsOpenSteps!Action & "," & Format(rsOpenSteps!Due, "mm/dd/yyyy")
                li = li + 1
            Case Is <= (Date + 7) 'due in next week
                If ni > 15 Then
                    ni = ni + 1
                    GoTo nextStep
                End If
                ReDim Preserve nextSteps(ni)
                nextSteps(ni) = rsOpenSteps!partNumber & "," & rsOpenSteps!Action & "," & Format(rsOpenSteps!Due, "mm/dd/yyyy")
                ni = ni + 1
        End Select
nextStep:
        rsOpenSteps.MoveNext
    Loop

    rsOpenSteps.CLOSE
    Set rsOpenSteps = Nothing
    
    Dim rsOpenIssues As Recordset
    Set rsOpenIssues = db.OpenRecordset("SELECT * FROM qryOpenIssues_summaryEmail WHERE inCharge = '" & rsPeople!User & "' AND closeDate is null AND dueDate <= Date()+7")
    
    Do While Not rsOpenIssues.EOF
        Select Case rsOpenIssues!dueDate
            Case Date 'due today
                ReDim Preserve todaySteps(ti)
                todaySteps(ti) = rsOpenIssues!partNumber & ",Open Issue: " & rsOpenIssues!issueType & "-" & rsOpenIssues!issueSource & ",Today"
                ti = ti + 1
            Case Is < Date 'over due
                ReDim Preserve lateSteps(li)
                lateSteps(li) = rsOpenIssues!partNumber & ",Open Issue: " & rsOpenIssues!issueType & "-" & rsOpenIssues!issueSource & "," & Format(rsOpenIssues!dueDate, "mm/dd/yyyy")
                li = li + 1
            Case Is <= (Date + 7) 'due in next week
                ReDim Preserve nextSteps(ni)
                nextSteps(ni) = rsOpenIssues!partNumber & ",Open Issue: " & rsOpenIssues!issueType & "-" & rsOpenIssues!issueSource & "," & Format(rsOpenIssues!dueDate, "mm/dd/yyyy")
                ni = ni + 1
        End Select
        rsOpenIssues.MoveNext
    Loop
    
    If ti + li + ni > 0 Then
        Set rsNoti = db.OpenRecordset("tblNotificationsSP")
        With rsNoti
            .addNew
            !recipientUser = rsPeople!User
            !recipientEmail = rsPeople!userEmail
            !senderUser = "workingDB"
            !senderEmail = "workingDB@us.nifco.com"
            !sentDate = Now()
            !readDate = Now()
            !notificationType = 9
            !notificationPriority = 2
            !notificationDescription = "Summary Email"
            !emailContent = StrQuoteReplace(dailySummary("Hi " & rsPeople!firstName, "Here is what you have going on...", lateSteps(), todaySteps(), nextSteps(), li, ti, ni))
            .Update
        End With
        rsNoti.CLOSE
        Set rsNoti = Nothing
    End If
    
nextPerson:
    rsPeople.MoveNext
Loop

rsUserSettings.CLOSE
Set rsUserSettings = Nothing
Set db = Nothing
grabSummaryInfo = True

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "grabSummaryInfo", Err.DESCRIPTION, Err.number)
End Function

Function checkProgramEvents() As Boolean
On Error GoTo Err_Handler

Dim db As Database
Set db = CurrentDb()

Dim rsProgram As Recordset, rsEvents As Recordset, rsWO As Recordset, rsComments As Recordset, rsPeople As Recordset, rsNoti As Recordset
Dim controlNum As Long, Comments As String, dueDate, body As String, strValues

dueDate = addWorkdays(Date, 5)

Set rsEvents = db.OpenRecordset("SELECT * from tblProgramEvents WHERE designWOcreated = False AND eventDate BETWEEN #" & Date & "# AND #" & Date + 50 & "#")
Set rsPeople = db.OpenRecordset("SELECT * from tblPermissions WHERE designWOid = 1 AND InActive = FALSE")

Do While Not rsEvents.EOF
    Set rsProgram = db.OpenRecordset("SELECT * from tblPrograms WHERE ID = " & rsEvents!programId)
    
    Set rsWO = db.OpenRecordset("dbo_tblDRS")
    rsWO.addNew
        With rsWO
            !Issue_Date = Date
            !Approval_Status = 1
            !Requester = "automated"
            !DR_Level = 1
            !Request_Type = 23
            !Design_Level = 4 'ETA
            !Due_Date = dueDate
            !Part_Number = "D8157"
            !PART_DESCRIPTION = "Program Review"
            !Model_Code = rsProgram!modelCode
        End With
    rsWO.Update
    
    controlNum = db.OpenRecordset("SELECT @@identity")(0).Value
    Comments = "'Hold program review for " & rsProgram!modelCode & " " & rsEvents!eventTitle & "'"
    
    db.Execute "INSERT INTO dbo_tblComments(Control_Number, Comments) VALUES(" & controlNum & "," & Comments & ")"
    
    body = emailContentGen("Program Review WO", "WO Notice", "WO Auto-Created for " & rsProgram!modelCode & " Program Review", "Event: " & rsEvents!eventTitle, "WO#" & controlNum, "Due: " & dueDate, "Sent On: " & CStr(Now()))
    
    rsEvents.Edit
    rsEvents!designWOcreated = True
    rsEvents.Update
    
    Set rsNoti = db.OpenRecordset("tblNotificationsSP")
    rsPeople.MoveFirst
    Do While Not rsPeople.EOF
        With rsNoti
            .addNew
            !recipientUser = rsPeople!User
            !recipientEmail = rsPeople!userEmail
            !senderUser = "automated"
            !senderEmail = "workingdb@us.nifco.com"
            !sentDate = Now()
            !notificationType = 10
            !notificationPriority = 2
            !notificationDescription = "WO Auto-Created for " & rsProgram!modelCode & " Program Review"
            !appName = "Design WO"
            !appId = controlNum
            !emailContent = body
            .Update
        End With
        rsPeople.MoveNext
    Loop
    
    rsNoti.CLOSE
    Set rsNoti = Nothing
        
    rsProgram.CLOSE
    Set rsProgram = Nothing
    
    rsEvents.MoveNext
Loop

rsEvents.CLOSE
Set rsEvents = Nothing

rsPeople.CLOSE
Set rsPeople = Nothing

Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "checkProgramEvents", Err.DESCRIPTION, Err.number)
End Function

Function getEmail(userName As String) As String
On Error GoTo Err_Handler

getEmail = ""
On Error GoTo tryOracle
Dim db As Database
Set db = CurrentDb()
Dim rsPermissions As Recordset
Set rsPermissions = db.OpenRecordset("SELECT * from tblPermissions WHERE user = '" & userName & "'")
getEmail = Nz(rsPermissions!userEmail, "")
rsPermissions.CLOSE
Set rsPermissions = Nothing

GoTo exitFunc

tryOracle:
Dim rsEmployee As Recordset
Set rsEmployee = db.OpenRecordset("SELECT FIRST_NAME, LAST_NAME, EMAIL_ADDRESS FROM APPS_XXCUS_USER_EMPLOYEES_V WHERE USER_NAME = '" & StrConv(userName, vbUpperCase) & "'")
getEmail = Nz(rsEmployee!EMAIL_ADDRESS, "")
rsEmployee.CLOSE
Set rsEmployee = Nothing

exitFunc:
Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "getEmail", Err.DESCRIPTION, Err.number)
End Function

Function splitString(A, B, C) As String
    On Error GoTo errorCatch
    splitString = Split(A, B)(C)
    Exit Function
errorCatch:
    splitString = ""
End Function

Function labelCycle(checkLabel As String, nameLabel As String, Optional controlSourceVal As String = "") As String()
On Error GoTo Err_Handler

    Dim returnVal(0 To 1) As String, sortLabel As String
    If controlSourceVal = "" Then
        sortLabel = nameLabel
    Else
        sortLabel = controlSourceVal
    End If
    Select Case True
        Case InStr(checkLabel, "-") > 0
            returnVal(0) = nameLabel & " >"
            returnVal(1) = sortLabel & " DESC"
        Case InStr(checkLabel, ">") > 0
            returnVal(0) = nameLabel & " <"
            returnVal(1) = sortLabel & " ASC"
        Case Else
            returnVal(0) = nameLabel & " -"
            returnVal(1) = sortLabel & " ASC"
    End Select
    labelCycle = returnVal
    
Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "labelCycle", Err.DESCRIPTION, Err.number)
End Function

Function idNAM(inputVal As Variant, typeVal As Variant) As Variant
On Error Resume Next 'just skip in case Oracle Errors
Dim db As Database
Set db = CurrentDb()
Dim rs1 As Recordset
idNAM = ""

If inputVal = "" Then Exit Function

If typeVal = "ID" Then
    Set rs1 = db.OpenRecordset("SELECT SEGMENT1 FROM APPS_MTL_SYSTEM_ITEMS WHERE INVENTORY_ITEM_ID = " & inputVal, dbOpenSnapshot)
    If rs1.RecordCount = 0 Then GoTo exitFunction
    idNAM = rs1("SEGMENT1")
End If

If typeVal = "NAM" Then
    Set rs1 = db.OpenRecordset("SELECT INVENTORY_ITEM_ID FROM APPS_MTL_SYSTEM_ITEMS WHERE SEGMENT1 = '" & inputVal & "'", dbOpenSnapshot)
    If rs1.RecordCount = 0 Then GoTo exitFunction
    idNAM = rs1("INVENTORY_ITEM_ID")
End If

exitFunction:
rs1.CLOSE
Set rs1 = Nothing
Set db = Nothing
End Function

Function getDescriptionFromId(inventId As Long) As String
On Error GoTo Err_Handler

Dim db As Database
Set db = CurrentDb()
Dim rs1 As Recordset

getDescriptionFromId = ""
If IsNull(inventId) Then Exit Function
On Error Resume Next

Set rs1 = db.OpenRecordset("SELECT DESCRIPTION FROM APPS_MTL_SYSTEM_ITEMS WHERE INVENTORY_ITEM_ID = " & inventId, dbOpenSnapshot)
If rs1.RecordCount = 0 Then GoTo exitFunction
getDescriptionFromId = rs1("DESCRIPTION")

exitFunction:
rs1.CLOSE
Set rs1 = Nothing
Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "getDescriptionFromId", Err.DESCRIPTION, Err.number)
End Function

Function getStatusFromId(inventId As Long) As String
On Error GoTo Err_Handler

Dim db As Database
Set db = CurrentDb()
Dim rs1 As Recordset

getStatusFromId = ""
If IsNull(inventId) Then Exit Function
On Error Resume Next

Set rs1 = db.OpenRecordset("SELECT INVENTORY_ITEM_STATUS_CODE FROM APPS_MTL_SYSTEM_ITEMS WHERE INVENTORY_ITEM_ID = " & inventId, dbOpenSnapshot)
If rs1.RecordCount = 0 Then GoTo exitFunction
getStatusFromId = rs1("INVENTORY_ITEM_STATUS_CODE")

exitFunction:
rs1.CLOSE
Set rs1 = Nothing
Set db = Nothing

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "getStatusFromId", Err.DESCRIPTION, Err.number)
End Function

Public Function StrQuoteReplace(strValue)
On Error GoTo Err_Handler

StrQuoteReplace = Replace(Nz(strValue, ""), "'", "''")

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "StrQuoteReplace", Err.DESCRIPTION, Err.number)
End Function

Public Function wdbEmail(ByVal strTo As String, ByVal strCC As String, ByVal strSubject As String, body As String) As Boolean
On Error GoTo Err_Handler
wdbEmail = True
    
Dim objEmail As Object

Set objEmail = CreateObject("outlook.Application")
Set objEmail = objEmail.CreateItem(0)

With objEmail
    .To = strTo
    .CC = strCC
    .subject = strSubject
    .htmlBody = body
    .display
End With

Set objEmail = Nothing
    
Exit Function
Err_Handler:
wdbEmail = False
    Call handleError("wdbGlobalFunctions", "wdbEmail", Err.DESCRIPTION, Err.number)
End Function

Function removeReferenceString(stringWithReference As String, Optional addBetween As String = "") As String
On Error GoTo Err_Handler

Dim tempString As String
tempString = stringWithReference

If InStr(stringWithReference, "(") Then tempString = Trim(Split(stringWithReference, "(")(0))
If InStr(stringWithReference, ")") Then tempString = tempString & addBetween & Trim(Split(stringWithReference, ")")(1))

removeReferenceString = tempString

Exit Function
Err_Handler:
    Call handleError("wdbGlobalFunctions", "removeReferenceString", Err.DESCRIPTION, Err.number)
End Function